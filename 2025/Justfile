YEAR := "2025"
BASE_URL := "https://adventofcode.com/2025"
COOKIE := env_var("COOKIE")
# the AoC day unlocks at midnight EST
TODAY := `printf '%-d' $(TZ=America/New_York date +%e)`

# show this list of commands
default:
  just --list

# create a new day folder
template-day num=TODAY template="gleam": && (template-readme num) (download-input num) (git-add num)
  cookiecutter --no-input templates/{{template}} day_num={{num}}
  touch $(just day-dir {{num}}){/answers.txt,/example.txt}

# Start part 1, template out a new day folder
part1 num=TODAY: (template-day num)

# Start part 2, update the readme with the part 2 text
part2 num=TODAY: (download-prompt num) (readme-problem-desc "2" num) && (git-add num)

[private]
git-add num:
  git add $(just day-dir {{num}})


# output the name of the day directory
# This is used to standardize naming and account for the lack of interpolation
[private]
day-dir num:
  @echo "day$(printf '%02d' {{num}})"


[private]
mk-day-dir num:
  mkdir -p $(just day-dir {{num}})


[private]
tmpdir:
  mkdir -p tmp


[private]
download-prompt num: tmpdir
  curl {{BASE_URL}}/day/{{num}} -H "Cookie: {{COOKIE}}" --fail > tmp/day{{num}}.html \
    || rm -f tmp/day{{num}}.html

# create a README.md in the day folder with the challenge text
template-readme num=TODAY: (download-prompt num)
  cat \
    <(just readme-day-title {{num}}) \
    <(echo) \
    <(just readme-problem-desc 1 {{num}}) \
    <(echo) \
    <(just readme-problem-desc 2 {{num}}) \
    | tee -a "$(just day-dir {{num}})/README.md"

[private]
readme-day-title num=TODAY:
  pup 'h2:contains("Day {{num}}")' text{} < tmp/day{{num}}.html \
    | sed 's/^---/\#/' \
    | sed 's/ ---$//' \

[private]
readme-problem-desc part num=TODAY:
  if [[ "{{part}}" == "1" ]]; then echo "## Part One"; fi
  pup --pre "article.day-desc:nth-of-type({{part}})" < tmp/day{{num}}.html \
    | pandoc -f html -t gfm \
    | sed '/# --- Day/d' \
    | sed 's/# ---//' \
    | sed 's/^# Part/## Part/' \
    | sed 's/ ---$//'

[private]
readme-clear-problem num=TODAY:
  sed -i '/\# Day 2:/,$d' "$(just day-dir {{num}})/README.md"

# download and save the input for the given day in input.txt
download-input num=TODAY:
  curl {{BASE_URL}}/day/{{num}}/input -H "Cookie: {{COOKIE}}" --fail > "$(just day-dir {{num}})/input.txt" \
    || rm -f "$(just day-dir {{num}})/input.txt"


[private]
test num:
  #!/usr/bin/env bash
  set -euxo pipefail
  pwd
  echo {{COOKIE}}


# remove temporary files
clean:
  rm -r tmp


# post an answer to the advent of code website and save it to answers.txt
answer part answer day=TODAY:
  curl --fail-with-body -X POST \
    -H "Content-Type: application/x-www-form-urlencoded" \
    -H "Cookie: {{COOKIE}}" \
    -d "answer={{answer}}&level={{part}}" \
    {{BASE_URL}}/day/{{day}}/answer \
  | pandoc -f html -t plain
  echo {{answer}} >> $(just day-dir {{day}})/answers.txt


# open the advent of code website for the given day
open day=TODAY:
  xdg-open {{BASE_URL}}/day/{{day}}
